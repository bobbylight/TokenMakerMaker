['java', 'checkstyle', 'distribution', 'maven', 'signing'].each { apply plugin: it }

group = 'com.fifesoft'
archivesBaseName = 'tokenMakerMaker'

dependencies {

    compile files("${projectDir.absolutePath}/../RSyntaxTextArea/RSyntaxTextArea/build/classes/java/main")
    compile files('lib/balloontip-1.2.1.jar', 'lib/fife.common.jar', 'lib/JFlex.jar', 'lib/miglayout-swing-5.0.jar')

	compile 'de.jflex:jflex:1.4.1'
    compile 'com.fifesoft.rtext:fife.common:2.6.3'
    compile 'com.fifesoft:rsyntaxtextarea:3.0.0-SNAPSHOT'
    compile 'com.miglayout:miglayout-swing:5.0'
    compile 'net.java.balloontip:balloontip:1.2.4.1'
	testCompile 'junit:junit:4.12'
}

wrapper {
	gradleVersion '5.0-rc-4'
}

checkstyle {
    toolVersion = '8.14'
    configFile = 'config/checkstyle/checkstyle.xml' as File
}

compileJava {
	sourceCompatibility javaVersion
	targetCompatibility javaVersion
	options.debug = true
	options.debugOptions.debugLevel = 'source,vars,lines'
	options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

ext.sharedManifest = manifest {
	attributes('Main-Class': 'org/fife/tmm/Main',
		'Class-Path': "balloontip-1.2.4.1.jar fife.common-${version}.jar jflex-1.4.1.jar miglayout-core-5.0.jar miglayout-swing-5.0.jar rsyntaxtextarea-${version}.jar",
		'Specification-Title': 'TokenMakerMaker',
		'Specification-Version': version,
		'Implementation-Title': 'org.fife.ui',
		'Implementation-Version': version)

}
jar {
	manifest { from sharedManifest }
}

// We use "distributions" to create the zip files uploaded to SourceForge
distributions {
	main {
		baseName = 'tmm'
		contents {
			from { [ "${projectDir.absolutePath}/build/libs", configurations.runtime ] }
			rename 'TokenMakerMaker-.*\\.jar', 'tmm.jar'
			//rename '(jflex-.*\\.jar)', 'lib/$1'
		}
	}
	src {
		baseName = 'tmm'
	}
}
distZip.classifier = null
distZip {
	from "$projectDir/lib"
	include '**'
	from "$projectDir/../RSyntaxTextArea/build/libs"
	include 'rsyntaxtextarea-${version}.jar'
	rename { return baseName + '-' + version + '/' + it }
}
distZip.dependsOn jar
srcDistZip.classifier = 'src'
srcDistZip {
	from projectDir
	include 'src/**'
	include 'lib/**'
	include 'build.gradle'
	include '.classpath'
	include '.project'
	include 'gradle.properties'
	include 'gradle/**/*'
	include 'gradlew*'
	include 'README.md'
	include '.settings/**'
}

task buildSourceForgeZips {
    doLast {
        println 'Building zip files for SourceForge'
    }
}
buildSourceForgeZips.dependsOn clean, jar, distZip, srcDistZip

// Build the application

// Stuff to generate and upload Maven artifacts
task javadocJar (type: Jar, dependsOn: javadoc) {
	manifest { from sharedManifest }
	classifier = 'javadoc'
	from javadoc.destinationDir
}
task sourceJar (type: Jar) {
	manifest { from sharedManifest }
	classifier = 'sources'
	from sourceSets.main.allSource
}
artifacts {
	archives jar, javadocJar, sourceJar
}
signing {
	// Don't require signing for e.g. ./gradlew install
	required { gradle.taskGraph.hasTask("uploadArchives") }
	sign configurations.archives
}
repositories {
	mavenCentral()
	maven { // Snapshot builds of RSTA artifacts
		url 'https://oss.sonatype.org/content/repositories/snapshots'
	}
}
uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			if (project.hasProperty('upload') && Boolean.parseBoolean(upload)) { // gradlew -Pupload=true
				repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
				snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
			}
			else {
				repository(url: 'file:///' + projectDir + '/../localMavenRepo')
			}
			pom {
				groupId = 'com.fifesoft'
				name = 'tokenmakermaker'
				project {
					parent {
						groupId 'org.sonatype.oss'
						artifactId 'oss-parent'
						version '7'
						relativePath ''
					}
					
					groupId 'com.fifesoft'
					artifactId 'tokenmakermaker'
					packaging 'jar'
					
					name 'TokenMakerMaker'
					description 'An application to easily create syntax highlighting for custom languages in RSyntaxTextArea.'
					
					inceptionYear '2003'
					url 'http://www.fifesoft.com/rsyntaxtextarea/'
					licenses {
						license {
							name 'Modified BSD License'
							url 'http://fifesoft.com/rsyntaxtextarea/RSyntaxTextArea.License.txt'
							distribution 'repo'
						}
					}
					
					scm {
						url 'https://github.com/bobbylight/TokenMakerMaker'
						connection 'scm:git:git://github.com/bobbylight/TokenMakerMaker'
						developerConnection 'scm:git:git@github.com:bobbylight/TokenMakerMaker'
						if (!project.version.endsWith('-SNAPSHOT')) {
							tag project.version
						}
					}
					
					developers {
						developer {
							name 'Robert Futrell'
							url 'http://www.fifesoft.com'
							organization = 'Fifesoft' /* https://issues.gradle.org/browse/GRADLE-1200 */
							organizationUrl 'http://www.fifesoft.com/'
							roles {
								role 'architect'
								role 'developer'
							}
							timezone '0'
						}
					}
					
					// Manually add runtime dependencies since we don't have
					// a multi-project build set up properly
					dependencies {
						dependency {
							groupId 'com.fifesoft'
							artifactId 'rsyntaxtextarea'
							version project.version
						}
					}
					
					// Says to enforce 1.6 compatibility when building
					// Doesn't work as of Gradle 2.0 due to a Groovy bug; see
					// http://jira.codehaus.org/browse/GROOVY-7023
					// For now, append it via XML directly
					/*
					build {
						plugins {
							plugin {
								artifactId 'maven-compiler-plugin'
								version '2.3.2'
								executions {
									execution {
										id 'default-compile'
										configuration {
											source '1.6'
											target '1.6'
										}
									}
								}
							}
						}
					}
					*/
					withXml {
						def pluginNode = asNode().appendNode('build').appendNode('plugins').appendNode('plugin')
						pluginNode.appendNode('artifactId', 'maven-compiler-plugin')
						pluginNode.appendNode('version', '2.3.2')
						def executionNode = pluginNode.appendNode('executions').appendNode('execution')
						executionNode.appendNode('id', 'default-compile')
						executionNode.appendNode('configuration').appendNode('source', '1.6').
						      parent().appendNode('target', '1.6')
					}
				}
			}
		}
	}
}
